
		model {
		
		# -------------------------------------------------
		# Parameters:
		# s: survival probability
		# psi1:psi5 transitions from defclass 1:5
		# -------------------------------------------------
		# States (S):
		# 1 high
		# 2 mod
		# 3 low
		# 4 vlow
		# 5 dead
		
		# -------------------------------------------------
		
		# Priors and constraints
		# Survival: uniform
		for(i in 1:nind) {
		for (t in first[i]:(last[i]-1)){ 
			logit(s[i,t]) <- alpha + beta*Rain[t] + gam*pfc[i,t] + zeta*log(tm[t])
			}
		}
		
		
		# Transitions:
		for (i in 1:4){
		for(t in 1:(n.occasions-1)) {
		lpsi1[i,t] <- eta1[i] + delta1[i]*Ko[t] + kappa1[i]*Rain[t]
		lpsi2[i,t] <- eta2[i] + delta2[i]*Ko[t] + kappa2[i]*Rain[t]
		lpsi3[i,t] <- eta3[i] + delta3[i]*Ko[t] + kappa3[i]*Rain[t]
		lpsi4[i,t] <- eta4[i] + delta4[i]*Ko[t] + kappa4[i]*Rain[t]
		
		psi1[i,t]<- epsi1[i,t]/sum(epsi1[,t])
		epsi1[i,t]<- exp(lpsi1[i,t])
		psi2[i,t]<- epsi2[i,t]/sum(epsi2[,t])
		epsi2[i,t]<- exp(lpsi2[i,t])
		psi3[i,t]<- epsi3[i,t]/sum(epsi3[,t])
		epsi3[i,t]<- exp(lpsi3[i,t])
		psi4[i,t]<- epsi4[i,t]/sum(epsi4[,t])
		epsi4[i,t]<- exp(lpsi4[i,t])
		}
		}
		
		alpha ~ dnorm(0, 0.1)
		beta ~ dnorm(0, 0.1)
		gam ~ dnorm(0, 0.1)
		zeta ~ dnorm(0, 0.1)

		# Constrain baseline transitions to 0: 
		# baseline is self transition 1->1; 2->2, 3->3,4->4
		
		eta1[1] <- 0
		for(i in 2:4) {eta1[i] ~ dnorm(0, 0.1)}
		eta2[2]<- 0
		for(i in c(1,3,4)) {eta2[i] ~ dnorm(0, 0.1)}
		eta3[3]<- 0
		for(i in c(1,2,4)) {eta3[i] ~ dnorm(0, 0.1)}
		eta4[4]<- 0
		for(i in 1:3) {eta4[i] ~ dnorm(0, 0.1)}
		
		
		delta1[1] <- 0
		for(i in 2:4) {delta1[i] ~ dnorm(0, 0.1)}
		delta2[2]<- 0
		for(i in c(1,3,4)) {delta2[i] ~ dnorm(0, 0.1)}
		delta3[3]<- 0
		for(i in c(1,2,4)) {delta3[i] ~ dnorm(0, 0.1)}
		delta4[4]<- 0
		for(i in 1:3) {delta4[i] ~ dnorm(0, 0.1)}
		
		kappa1[1] <- 0
		for(i in 2:4) {kappa1[i] ~ dnorm(0, 0.1)}
		kappa2[2]<- 0
		for(i in c(1,3,4)) {kappa2[i] ~ dnorm(0, 0.1)}
		kappa3[3]<- 0
		for(i in c(1,2,4)) {kappa3[i] ~ dnorm(0, 0.1)}
		kappa4[4]<- 0
		for(i in 1:3) {kappa4[i] ~ dnorm(0, 0.1)}
		
		# Define transition matrix 	
		for (i in 1:nind){
		for (t in first[i]:(last[i]-1)){
		ps[1,i,t,1] <- s[i,t] * psi1[1,t]
		ps[1,i,t,2] <- s[i,t] * psi1[2,t]
		ps[1,i,t,3] <- s[i,t] * psi1[3,t]
		ps[1,i,t,4] <- s[i,t] * psi1[4,t]
		ps[1,i,t,5] <- 1-s[i,t]
		ps[2,i,t,1] <- s[i,t] * psi2[1,t] 
		ps[2,i,t,2] <- s[i,t] * psi2[2,t]
		ps[2,i,t,3] <- s[i,t] * psi2[3,t]
		ps[2,i,t,4] <- s[i,t] * psi2[4,t]
		ps[2,i,t,5] <- 1-s[i,t] 
		ps[3,i,t,1] <- s[i,t] * psi3[1,t] 
		ps[3,i,t,2] <- s[i,t] * psi3[2,t]
		ps[3,i,t,3] <- s[i,t] * psi3[3,t]
		ps[3,i,t,4] <- s[i,t] * psi3[4,t]
		ps[3,i,t,5] <- 1-s[i,t] 
		ps[4,i,t,1] <- s[i,t] * psi4[1,t] 
		ps[4,i,t,2] <- s[i,t] * psi4[2,t]
		ps[4,i,t,3] <- s[i,t] * psi4[3,t]
		ps[4,i,t,4] <- s[i,t] * psi4[4,t]
		ps[4,i,t,5] <- 1-s[i,t] 
		ps[5,i,t,1] <- 0 
		ps[5,i,t,2] <- 0
		ps[5,i,t,3] <- 0
		ps[5,i,t,4] <- 0
		ps[5,i,t,5] <- 1
		} #t
		} #i
		
		# Likelihood 
		for (i in 1:nind){
		for (t in (first[i]+1):last[i]){
		# transition process
		y[i,t] ~ dcat(ps[y[i,t-1], i, t-1,])
		} #t
		} #i
		}
		
